defmodule AshJido.Context do
  @moduledoc false

  @optional_passthrough_keys [:authorize?, :tracer, :scope, :context, :timeout]

  @spec extract_ash_opts!(map(), module(), atom()) :: keyword()
  def extract_ash_opts!(context, resource, action_name) when is_map(context) do
    domain = require_domain!(context, resource, action_name)

    context
    |> base_opts(domain)
    |> maybe_add_optional_passthroughs(context)
  end

  defp base_opts(context, domain) do
    [domain: domain]
    |> maybe_add_if_present(context, :actor)
    |> maybe_add_if_present(context, :tenant)
  end

  defp maybe_add_optional_passthroughs(ash_opts, context) do
    Enum.reduce(@optional_passthrough_keys, ash_opts, fn key, opts ->
      maybe_add_if_present(opts, context, key)
    end)
  end

  defp maybe_add_if_present(opts, context, key) do
    if Map.has_key?(context, key) do
      Keyword.put(opts, key, Map.get(context, key))
    else
      opts
    end
  end

  defp require_domain!(context, resource, _action_name) do
    case Map.get(context, :domain) do
      nil -> Ash.Resource.Info.domain(resource)
      domain -> domain
    end
  end
end
